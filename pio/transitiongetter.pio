;
; Transition getter - needed for finding 0xDA -> 0xF2 transition timings.
;
; For 48 MHz timings this must be run at 96 MHz (per Nyquist theorem).
;
; Wait pin is POST bit 0
; Jmp pin is POST bit 5 (0xDA->0xF2 will set the bit to 1)
;
; The output counter starts at 0 and decrements from there (0, -1, -2, -3...).
; Conversion of 2's complement gives the actual timing.
;

.program transitiongetter
    set y,0                     ; remember that y is 32-bit!

    wait 1 pin 0                ; wait for POST 0xD7
    wait 0 pin 0                ; wait for POST 0xD8
    wait 1 pin 0                ; wait for POST 0xD9
    wait 0 pin 0                ; wait for POST 0xDA

    jmp y--, reset_delay        ; underflow y
reset_delay:
    jmp pin, done               ; loop until POST bit goes 1
    jmp y--, reset_delay        ; otherwise decrement y
    ; execution should NOT fall through

done:
    mov isr,y
    push noblock

    .wrap_target
    nop
    .wrap

